<<*>>=
global SEMANTICS, IDENT, STARTSEM, ENDSEM
procedure scantokens()
  static alphanum, semantics, semmatch
  initial { 
    alphanum := &letters ++ &digits ++ '_'
    SEMANTICS := " semantics "
    STARTSEM := " start of semantics "
    ENDSEM := " end of semantics "
    IDENT := " identifier "
    semmatch := table()
    semmatch["/."] := "./"
    semmatch["/*"] := "*/"
  }
  repeat {
    if /semantics then {
      tab(many(' \t'))
      if ="#" | pos(0) then {
        fail
      } else if any(&letters) & tval := tab(many(alphanum)) then {
        token := IDENT
      } else if semantics := tval := =key(semmatch) then {
        token := STARTSEM
      } else if token := =("'"|"\"") then {
        tval := tab(upto(token)) | error("unclosed ", token)
        move(1)
      } else if pos(1) & tval := ="%%" & pos(0) then {
        token := "%%"
      } else {
        token := tval := move(1)
      }
      thispos := &pos
      suspend token
    } else {
      if tval := tab(find(semmatch[semantics])) then {
        suspend token := SEMANTICS
        tval := =semmatch[semantics] | stop("impossible")
        suspend token := ENDSEM
        semantics := &null
      } else if pos(0) then {
        fail
      } else {
        tval := tab(0)
        suspend token := SEMANTICS
      }
    }
  }
end
