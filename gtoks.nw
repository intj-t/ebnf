<<*>>=
global SEMANTICS, IDENT
procedure scantokens()
  static alphanum, semantics
  initial { 
    alphanum := &letters ++ &digits ++ '_'
    SEMANTICS := " semantics "
    IDENT := " identifier "
  }
  repeat {
    if /semantics then {
      tab(many(' \t'))
      if ="#" | pos(0) then {
        fail
      } else if any(&letters) & tval := tab(many(alphanum)) then {
        token := IDENT
      } else if token := ="/." then {
        tval := semantics := token
      } else if token := =("'"|"\"") then {
        tval := tab(upto(token)) | error("unclosed ", token)
        move(1)
      } else if pos(1) & tval := ="%%" & pos(0) then {
        token := "%%"
      } else {
        token := tval := move(1)
      }
      thispos := &pos
      suspend token
    } else {
      if tval := tab(find("./")) then {
        suspend token := SEMANTICS
        suspend token := tval := ="./" | stop("impossible")
        semantics := &null
      } else {
        tval := tab(0)
        suspend token := SEMANTICS
      }
    }
  }
end
