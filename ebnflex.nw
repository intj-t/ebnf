\section{Generic lexer and error routines for use with EBNF parser generator}
This lexer expects two external procedures: 
[[scantokens]] generates all the tokens in the current [[&subject]].
It exports the following:
\begin{itemize} 
\item[\tt lex(file)] 
reads the next token from file [[file]], setting
[[token]] and [[tval]]. 
If [[file]] is omitted, it defaults to the file supplied on the
previous call to [[lex]], or to [[&input]].
\item[\tt gobble(t)] 
is [[if token == t then { lex(); return t }]]
\item[\tt expect(t)]
is [[gobble(t) | ]]{\em error message}
\item[\tt error(...)]
prints an error message and halts.
\item[\tt Ident()] returns an identifier from the input stream.
\item[\tt Int()] returns an integer from the input stream.
\end{itemize}
@
[[token]] describes the category of the token, and [[tval]] is its value.
[[token == tval]] except for integers and identifiers.
[[filename]] and [[lineno]] describe the source file and line number from which 
the token came.
<<*>>=
global token, tval, filename, lineno
global thisline, thispos
@
There are a few predefined tokens.
<<*>>=
global EOF
@
[[lex]] sets the new [[token]] and [[tval]], returning [[token]].
<<*>>=
global pass_through_resynch
procedure lex(newfile)
  static toks,  infile
  local eol
  initial {EOF    := " end of file "; /lineno := 0; toks := [];
           infile := &input; filename := "standard input"}
  filename := image(infile := \newfile)[6:-1]
  if \newfile then lineno := 0
  if *toks = 1 & token := scantokens(pop(toks)) then
    return token
  else if *toks = 2 & (pop(toks) ? { 
      tab(thispos := pop(toks))
      if token := scantokens() then {
        every push(toks, if pos(0) then 1 else &pos | &subject)
        return token
      } 
    }) then &null
  else {
    while thisline := read(infile) do {
      lineno +:= 1
      thispos := 1
      thisline ?    
        if (="#line ", /pass_through_resynch, lineno := integer(tab(many(&digits)))-1, 
            =" \"", filename := tab(upto('"')), ="\"", pos(0)) |
           (="<* LINE ", /pass_through_resynch, lineno :=integer(tab(many(&digits)))-1,
            =" \"", filename := tab(upto('"')), ="\" *>", pos(0)) then { }
        else {
          if token := scantokens() then {
            every push(toks, if pos(0) then 1 else &pos | &subject)
            return token
          }
        }
    }
  }
  return token := tval := EOF
end
@
Many programs will use one of these two procedures much of the time.
[[gobble]] consumes a token, failing if it is not present.
[[expect]] consumes a token, printing an error message if it is not present.
<<*>>=
procedure gobble(t)
    if token == t then return 1(.tval, lex())
end

procedure expect(t, nt)
    return gobble(t) | 
    error("when parsing "||\nt||", " | "", 
          "expected ", image(t), " but found ", image(tval))
end

procedure lexwhere(outfile)
  /outfile := &errout
  write(outfile, thisline)
  write(outfile, repl(" ", \thispos-1), "^", repl(" ", 0 <= *thisline-thispos) | "", "$")
  return
end

procedure erroratfl(file, line, L[])
  write ! ([&errout, image(filename), ", line ", lineno, ": "] ||| L)
  stop()
end

procedure errorat(loc, L[])
  write ! ([&errout, loc, ": "] ||| L)
  stop()
end

procedure error(L[])
  <<write error output with position>>
  stop()
end

procedure warning(L[])
  write ! ([&errout, image(filename), ", line ", lineno, ": Warning -- "] ||| L)
end

procedure impossible(L[])
  push(L, "This can't happen: ")
  <<write error output with position>>
  write(&errout, "\n")
  &null[0]
end
<<write error output with position>>=
write ! ([&errout, image(filename), ", line ", lineno, ": "] ||| L)
lexwhere()
write(&errout, "token = ", token, " [", image(tval), "]")
